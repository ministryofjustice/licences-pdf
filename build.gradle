import java.time.Instant
import java.time.LocalDate
import java.time.format.DateTimeFormatter

buildscript {
    ext {
        springBootVersion = '2.1.5.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'groovy'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    mavenCentral()
}

ext {
    springCloudVersion = 'Finchley.RELEASE'
}

def today = Instant.now()
group = 'uk.gov.justice.digital.hmpps'
project.sourceCompatibility = 11
project.targetCompatibility = 11

def todayDate = LocalDate.now()
def formatter = DateTimeFormatter.ofPattern('yyyy-MM-dd')
version todayDate.format(formatter)

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version = System.getenv('CI') ? "${version}.${System.getenv('CIRCLE_BUILD_NUM')}" : version
            group = 'uk.gov.justice.digital.hmpps'
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    implementation 'net.logstash.logback:logstash-logback-encoder:5.3'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:1.2.0-BETA'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.3.1'
    implementation 'org.apache.commons:commons-text:1.6'
    implementation 'commons-io:commons-io:2.6'
    implementation 'com.github.timpeeters:spring-boot-graceful-shutdown:2.1.1'

    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'org.xhtmlrenderer:flying-saucer-pdf-itext5:9.1.18'
    implementation 'com.mashape.unirest:unirest-java:1.4.9'

    compileOnly 'org.projectlombok:lombok:1.18.8'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.sun.xml.bind:jaxb-impl:2.3.2'
    implementation 'com.sun.xml.bind:jaxb-core:2.3.0.1'
    implementation 'javax.activation:activation:1.1.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'

    testImplementation 'org.codehaus.groovy:groovy-all:2.5.4'
    testImplementation 'org.spockframework:spock-spring:1.2-groovy-2.5'
    testImplementation('org.spockframework:spock-core:1.2-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
}
